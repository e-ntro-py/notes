use crate::env::{Env, Ty};

#[LALR] grammar;

SepBy<T, S>: Vec<T> = {
    <v:(<T> S)*> <e:T?> => match e {
        None => v,
        Some(e) => {
            let mut v = v;
            v.push(e);
            v
        }
    }
}

pub P: Env = {
    <decls:SepBy<D, ";">> => Env::new(decls),
}

pub D: (Ty, String) = {
    <t:T> <s:Id> => (t, s),
}

pub T: Ty = {
    <b:B> <c:C> => Ty::array(b, c),
    "record" "{" <env:P> "}" => Ty::record(env),
}

B: Ty = {
    "int" => Ty::Int,
    "float" => Ty::Float,
}

C: Vec<usize> = {
    <dims:Dim*> => dims,
}

Dim: usize = {
    "[" <s:r"\d+"> "]" => s.parse().unwrap(),
}

Id: String = {
    <s:r"[a-zA-Z]\w*"> => s.to_owned(),
}