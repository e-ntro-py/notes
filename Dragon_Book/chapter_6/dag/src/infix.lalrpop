use crate::symbolic::{BinOp, UnOp, Expr, Stmt, Stmts, Access, LValue, Assign, Boolean, RelOp};
use std::rc::Rc;

#[LALR] grammar;

pub P: Stmts = {
    <stmts:S*> => Stmts::new(stmts),
}

S: Rc<Stmt> = {
    <a:A> ";" => Stmt::assign(a),
    "if" "(" <BOr> ")" "{" <P> "}" => Stmt::if_only(<>),
    "if" "(" <BOr> ")" "{" <P> "}" "else" "{" <P> "}" => Stmt::if_else(<>),
    "while" "(" <BOr> ")" "{" <P> "}" => Stmt::while_clause(<>),
    "for" "(" <A> ";" <BOr> ";" <A> ")" "{" <P> "}" => Stmt::for_clause(<>),
}

A: Assign = {
    <lvalue:LV> "=" <rvalue:E> => Assign { lvalue, rvalue },
    <lvalue:LV> "++" => {
        let expr = Expr::from(lvalue.clone()).dedup();
        let rvalue = Expr::bin(BinOp::Add, expr, Expr::lit(1));
        Assign { lvalue, rvalue }
    }
}

LV: LValue = {
    <id:Id> => LValue::Var(id),
    <l:L> => LValue::Access(l),
}

pub E: Rc<Expr> = {
    <e:E> "+" <t:T> => Expr::bin(BinOp::Add, e, t),
    <e:E> "-" <t:T> => Expr::bin(BinOp::Sub, e, t),
    <t:T> => t,
}

T: Rc<Expr> = {
    <t:T> "*" <f:F> => Expr::bin(BinOp::Mul, t, f),
    <f:F> => f,
}

F: Rc<Expr> = {
    "-" <f:F> => Expr::un(UnOp::Neg, f),
    "+" <f:F> => Expr::un(UnOp::Pos, f),
    <l:L> => Expr::access(l),
    "(" <e:E> ")" => e,
    <id:Id> => Expr::var(id),
    <n:Num> => Expr::lit(n),
    <f:Float> => Expr::float(f),
    "TRUE" => Expr::lit(1),
    "FALSE" => Expr::lit(0),
}

L: Access = {
    <var:Id> <dims:Dim+> => Access { var, dims },
}

Dim: Rc<Expr> = {
    "[" <e:E> "]" => e,
}

Num: usize = {
    <s:r"\d+"> => s.parse().unwrap(),
}

Float: f64 = {
    <s:r"\d+\.\d+"> => s.parse::<f64>().unwrap(),
}

Id: String = {
    <s:r"[a-zA-Z]\w*"> => s.to_owned(),
}

BOr: Rc<Boolean> = {
    <l:BOr> "||" <r:BAnd> => Boolean::or(l, r),
    <b:BAnd> => b,
}

BAnd: Rc<Boolean> = {
    <l:BAnd> "&&" <r:BFactor> => Boolean::and(l, r),
    <f:BFactor> => f, 
}

BFactor: Rc<Boolean> = {
    "!" <b:BFactor> => Boolean::not(b),
    <l:E> <op:Rel> <r:E> => Boolean::rel(op, l, r),
    "(" <b:BOr> ")" => b,
    "true" => Boolean::t(),
    "false" => Boolean::f(),
}

Rel: RelOp = {
    "==" => RelOp::Eq,
    "!=" => RelOp::Ne,
    ">" => RelOp::Gt,
    ">=" => RelOp::Ge,
    "<" => RelOp::Lt,
    "<=" => RelOp::Le,
}